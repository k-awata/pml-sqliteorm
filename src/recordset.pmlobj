-- Copyright (c) 2022 K.Awata
-- 
-- Permission is hereby granted, free of charge, to any person obtaining a copy
-- of this software and associated documentation files (the "Software"), to deal
-- in the Software without restriction, including without limitation the rights
-- to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
-- copies of the Software, and to permit persons to whom the Software is
-- furnished to do so, subject to the following conditions:
-- 
-- The above copyright notice and this permission notice shall be included in all
-- copies or substantial portions of the Software.
-- 
-- THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
-- IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
-- FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
-- AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
-- LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
-- OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
-- SOFTWARE.

-- Object RECORDSET stores tabular data which has columns and rows.
define object RECORDSET
    member .columns is ARRAY
    member .records is ARRAY
endobject

-- Constructor sets column names and records data.
define method .recordset(!columns is ARRAY, !records is ARRAY)
    !this.columns = !columns
    !this.records = !records
endmethod

-- Append appends new record from a STRINGMAP.
define method .Append(!record is STRINGMAP)
    -- Set columns if appending first record
    if !this.columns.Empty() then
        !this.columns = !record.keys
        !this.records.Clear()
    endif
    !values = object ARRAY()
    do !key values !this.columns
        !values.Append(!record.Get(!key))
        handle any
            !values.Append('')
        endhandle
    enddo
    !this.records.Append(!values)
endmethod

-- StringMaps returns an array of STRINGMAP.
define method .StringMaps() is ARRAY
    !data = object ARRAY()
    do !r values !this.records
        !data.Append(object STRINGMAP(!this.columns, !r))
    enddo
    return !data
endmethod

-- First returns STRINGMAP of the first record.
define method .First() is STRINGMAP
    !result = object STRINGMAP(!this.columns, !this.records.First())
    handle any
        return object STRINGMAP()
    endhandle
    return !result
endmethod

-- Column returns an array of values in a specified column.
define method .Column(!key is STRING) is ARRAY
    !index = !this.columns.FindFirst(!key)
    if !index.Unset() then
        return object ARRAY()
    endif
    !result = object ARRAY()
    do !r values !this.records
        !result.Append(!r[!index])
    enddo
    return !result
endmethod
