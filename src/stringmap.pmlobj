-- Copyright (c) 2022 K.Awata
-- 
-- Permission is hereby granted, free of charge, to any person obtaining a copy
-- of this software and associated documentation files (the "Software"), to deal
-- in the Software without restriction, including without limitation the rights
-- to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
-- copies of the Software, and to permit persons to whom the Software is
-- furnished to do so, subject to the following conditions:
-- 
-- The above copyright notice and this permission notice shall be included in all
-- copies or substantial portions of the Software.
-- 
-- THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
-- IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
-- FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
-- AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
-- LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
-- OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
-- SOFTWARE.

-- Object STRINGMAP stores data in key and value pairs
define object STRINGMAP
    member .keys   is ARRAY
    member .values is ARRAY
endobject

-- Constructor sets key names and the values.
define method .stringmap(!keys is ARRAY, !values is ARRAY)
    !this.keys = !keys
    !this.values = !values
endmethod

-- Get returns the value of the specified key as STRING.
define method .Get(!key is STRING) is STRING
    !index = !this.keys.FindFirst(!key)
    if !index.Unset() then
        return error 1 'Undefined key'
    endif
    return !this.values[!index]
endmethod

-- GetReal returns the value of the specified key as REAL.
define method .GetReal(!key is STRING) is REAL
    return !this.Get(!key).Real()
endmethod

-- GetBoolean returns the value of the specified key as BOOLEAN.
define method .GetBoolean(!key is STRING) is BOOLEAN
    !bool = !this.GetReal(!key).Boolean()
    handle any
        !bool = !this.Get(!key).Boolean()
    endhandle
    return !bool
endmethod

-- GetDateTime returns the value of the specified key as DATETIME.
define method .GetDateTime(!key is STRING) is DATETIME
    !dateTime = !this.Get(!key)
    !date = !dateTime.Before(' ')
    !time = !dateTime.After(' ')
    if !date.Match(':') ne 0 then
        !date = ''
        !time = !dateTime
    endif

    !year = !date.Part(1, '-').Real()
    if !year.Unset() then
        !year = 1901
    endif
    !month = !date.Part(2, '-').Real()
    if !month.Unset() then
        !month = 1
    endif
    !day = !date.Part(3, '-').Real()
    if !day.Unset() then
        !day = 1
    endif
    !hour = !time.Part(1, ':').Real()
    if !hour.Unset() then
        !hour = 0
    endif
    !minute = !time.Part(2, ':').Real()
    if !minute.Unset() then
        !minute = 0
    endif
    !second = !time.Part(3, ':').Real()
    if !second.Unset() then
        !second = 0
    endif

    return object DATETIME(!year, !month, !day, !hour, !minute, !second)
endmethod

-- Has returns whether this object has the specified key.
define method .Has(!key is STRING) is BOOLEAN
    return !this.keys.FindFirst(!key).Set()
endmethod

-- Set sets a new key and value, or overwrites the value of an existing key.
define method .Set(!key is STRING, !value is STRING)
    !index = !this.keys.FindFirst(!key)
    if !index.Set() then
        !this.values[!index] = !value
    else
        !this.keys.Append(!key)
        !this.values.Append(!value)
    endif
endmethod

-- SetDateTime converts DATETIME to STRING and then sets a new key and value,
-- or overwrites the value of an existing key.
define method .SetDateTime(!key is STRING, !value is DATETIME)
    !this.Set(!key, !value.Year().String() $
        + '-' + Substring('0' + !value.Month().String(), -2, 2) $
        + '-' + Substring('0' + !value.Date().String(), -2, 2) $
        + ' ' + Substring('0' + !value.Hour().String(), -2, 2) $
        + ':' + Substring('0' + !value.Minute().String(), -2, 2) $
        + ':' + Substring('0' + !value.Second().String(), -2, 2) $
    )
endmethod

-- Remove removes a key and the value and returns the value as STRING.
define method .Remove(!key is STRING) is STRING
    !index = !this.keys.FindFirst(!key)
    if !index.Unset() then
        return error 1 'Undefined key'
    endif
    !this.keys.Remove(!index)
    return !this.values.Remove(!index)
endmethod

-- Eq returns whether two STRINGMAPs have the same keys and values.
define method .Eq(!comp is STRINGMAP) is BOOLEAN
    do !i to !this.Size()
        !j = !comp.keys.FindFirst(!this.keys[!i])
        if !j.Unset() or !this.values[!i] ne !comp.values[!j] then
            return false
        endif
    enddo
    return true
endmethod

-- Empty returns whether this object has no key and value pairs.
define method .Empty() is BOOLEAN
    return !this.keys.Empty()
endmethod

-- Size returns the number of key and value pairs in this object.
define method .Size() is REAL
    return !this.keys.Size()
endmethod

-- Json returns JSON format STRING.
define method .Json() is STRING
    !json = '{'
    do !i to !this.Size()
        !json = !json + |"| $
            + !this.keys[!i].Replace('\', '\\').Replace(|"|, |\"|) + |":"| $
            + !this.values[!i].Replace('\', '\\').Replace(|"|, |\"|) + |"|
        skip if !i eq !this.Size()
        !json = !json + ','
    enddo
    !json = !json + '}'
    return !json
endmethod

-- ExpandedJson returns expanded JSON format STRING.
define method .ExpandedJson() is STRING
    !json = '{
'
    do !i to !this.Size()
        !json = !json + |    "| $
            + !this.keys[!i].Replace('\', '\\').Replace(|"|, |\"|) + |": "| $
            + !this.values[!i].Replace('\', '\\').Replace(|"|, |\"|) + |"|
        skip if !i eq !this.Size()
        !json = !json + ',
'
    enddo
    !json = !json + '
}'
    return !json
endmethod
